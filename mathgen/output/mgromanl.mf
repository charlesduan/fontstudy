% THIS IS THE OFFICIAL COMPUTER MODERN SOURCE FILE romanl.mf BY D E KNUTH.
% IT MUST NOT BE MODIFIED IN ANY WAY UNLESS THE FILE NAME IS CHANGED!

% Computer Modern Roman lower case:
% These letters were originally coded by D. E. Knuth in November, 1979,
% inspired by the Monotype faces used in {\sl The Art of Computer Programming}.
% Sans serif designs by Richard Southall were added in April, 1982.
% The programs were revised for the new \MF\ conventions in 1985.

% Character codes \0141 through \0172 are generated.

cmchar "The letter a";
beginchar("a",9u#,x_height#,0);
path p[];
bh#:=min(bar_height#,1.14x_height#-bar_height#); define_pixels(bh);
italcorr 1/3[bh#,x_height#]*slant+.5stem#-serif_fit#-2u#;
adjust_fit(0,serif_fit# if serifs: if hair#+.5stem#>1.5u#:-.25u# fi\\fi);
y3r=h+vround 1.5oo;
if serifs: pos1(flare,180); pos2(hair,180);
 pos3(vair,90);  x1r=hround max(u,2.1u-.5flare); x3=.5w-.5u;
 y1=min(bh+.5flare+2vair+2,.9[bh,h]-.5flare);
 p1=bulb_path(3,2,1,right);  % bulb
else: pos1(5/7[vair,flare],95); x1l=good.x 1.5u; x1r:=good.x x1r;
 pos3(1/8[vair,thin_join],90);
 x3=.5w-.2u; y1r=vround .82[bh,y3r];
 %make_paths1(rev.l) term.e(3,1,left,.9,4); fi  % terminal
 %p1=rpath1--lpath2; % TODO: should use fine pen end
fi
pos4(stem,0); x4r=hround(w-2.5u+.5stem); y4=1/3[bh,h];
pos5(stem,0); x5=x4; y5=max(.55bh,2vair);
make_paths2(rev.r) super_arc.e(3,4)&z4e..z5e;  % arc and stem
pos6(.3[thin_join,vair],90); x6=x4; y6=bh;
pos7(hround(curve-2stem_corr),180);
x7r=0; y7=1/3[y8l,y6r];
pos8(vair,270); x8l=.5w-.75u; y8r=-oo;
pos9(thin_join,360); z9l=z5l;
(x,y8r)=whatever[z8l,z9l]; x8r:=max(x,x8-u);
{{interim superness:=more_super;
 make_paths3(rev.r) z9e{down}...z8e{left}...{up}z7e&super_arc.e(7,6)}}; % bowl
if serifs: numeric shaved_stem; shaved_stem=hround(stem-3stem_corr);
 if hair#+.5stem#>1.5u#:
  pos5'(shaved_stem,0); x5'r=x5r; y5'=y5;
  pos10(shaved_stem,0); x10=x5'; y10=.2[.5tiny,bh];
  pos11(shaved_stem,0); x11r=w; y11=0;
  pos12(shaved_stem,0); x11=x12; y12=slab+eps;
  p2=z5'l---z10l...z11l{right}--pen_end(right,z11r,tiny,z12r,left){left}
   ...z10r+.75(z12-z11)---z5'r;  % foot
 else: pickup crisp.nib; pos5'(shaved_stem,0); x5'r=x5r; y5'=y5;
  pos10(shaved_stem,0); x10=x5'; y10=1/3bh;
  pos11(.2[vair,stem],90); x11r=.5[x10r,x12r]; y11l=-vround .5oo;
  pos12(hair,180); x12l=w; y12=max(y10,y11+vair);
  pos13(hair,180); x13=x12; y13=max(vround .6bh,y12);
  (x',y11l)=whatever[z11r,z12r]; x11l:=max(x',x10);
  p2=z5'l---z10l...z11l{right}...z12l
    ---{up}pen_lrend13(up,crisp,down){down}
    ---z12r...{left}z11r...z10r---z5'r;
  fi
else:
 numeric shaved_stem; shaved_stem=hround(stem-stem_corr);
 pos5'(shaved_stem,0); x5'r=x5r; y5'=y5;
 pos10(shaved_stem,0); x10=x5'; y10=0;
 p2=z5'l--pen_lrend10(down,tiny,up)--z5'r;
fi  % base of stem
p3=combine(combine(z4l--z5l,rpath3),p2);
% p1 is the upper terminal
% *path2 is the upper arc
fill rpath2 & p1 & combine(lpath2,p3)--cycle;
unfill combine(lpath3, (x9l,y4l)--z9l) & cycle;
penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13); endchar;

cmchar "The letter b";
beginchar("b",10u#+serif_fit#,asc_height#,0);
italcorr .5x_height#*slant+min(.5curve#-u#,-.25u#);
path p[];
adjust_fit(serif_fit#,0);
pos1(stem',0); pos2(stem,0);
pos0'(stem',0); pos0(stem,0); z0l=z0'l; x0'=x1; x0=x2;
x1l=jut; y1=h;
numeric edge; edge=x2r;
pos3(if hefty:thin_join else: hair fi,180);
pos4(vair,90); pos5(curve,0); pos6(vair,-90); penpos7(x3l-x3r,-180);
x3l=max(x3l-(x3r-x2l),1/3[x2,edge]);
y3=1/8[bar_height,x_height];
x4l=w-.5(w-serif_fit)+.5u; y4r=x_height+oo;
x5r=w; y5=.5x_height;
x6l=x4l-.2u; y6r=-oo;
x7=x3; y7=min(y3,y6+y4-y3+.6vair);
(x,y4r)=whatever[z3l,z4l]; x4r:=min(x,.5[x4,x5r]);
(x',y6r)=whatever[z7l,z6l]; x6r:=min(x',.5[x6,x5r]);
make_paths1(rev.r) z3e{up}...pulled_arc.e(4,5); % bowl top
make_paths2(rev.r) pulled_arc.e(5,6)...{up}z7e; % bowl bottom
y0=ypart(((edge,h)--(edge,0))intersectionpoint(z3l{up}...{right}z4l));
y2=ypart(((edge,h)--(edge,0))intersectionpoint(z6l{left}...{up}z7l));
pos8(hair,0);
x8l=x2l; bot y8=0;
p1=pen_lrend8(down,crisp,z2r-z8r)--z2r;
if serifs:
 p2=sloped_serif_path.l(1,0',a,1/3,jut,serif_drop); % upper serif
else:
 p2=pen_rlend1(up,tiny,down);
fi
fill combine(p1,rpath2) & combine(rpath1,z0'r--p2)--cycle;
if x3l>=x0r:
 unfill lpath1 & lpath2--cycle;
else:
 p3=combine(lpath2, (x0r,0)--(x0r,h));
 unfill combine(p3,lpath1) & cycle;
fi
penlabels(0,1,2,3,4,5,6,7,8); endchar;

cmchar "The letter c";
beginchar("c",8u#,x_height#,0);
italcorr x_height#*slant-.2u#;
adjust_fit(if monospace: .5u#,.5u# else: 0,0 fi);
path p[];
pos2(vair',90); pos4(vair',270);
x2=x4=.5(w+u); y2r=vround(h+1.5oo); y4r=-oo;
pos3(curve,180); x3r=0; y3=.5h;
if serifs: pos1(hair,0); pos0(flare,0);
 y1=min(bar_height+.5flare+2vair'+2,.9[bar_height,h]-.5flare);
 pos5(hair,0); x5r=w;
 x1r=x5r-hround(.2u);
 p1=bulb_path(2,1,0, right);  % bulb
 y5=max(good.y(.5bar_height-.9),y4l+vair');
 (x,y4l)=whatever[z4r,z5l]; x4l:=min(x,x4l+.5u);
 make_paths1(rev.l) pulled_super_arc.e(2,3)(.7superpull)
  & pulled_super_arc.e(3,4)(.5superpull)
  ..tension .9 and 1..{x5-x4,5(y5-y4)}z5e;  % arc and lower terminal
 fill lpath1 & p1 & rpath1--cycle;
else: pos1(4/7[vair',flare],80);
 x1r=hround(w-.6u); y1r=vround .82[bar_height,y2r];
 make_paths1(rev.r) term.e(2,1,right,.8,4);  % upper terminal
 p1=lpath1--rpath1;
 pos5(.6[vair',flare],275); x5r=hround(w-.5u);
 y5r=good.y(y5r+1/3bar_height-y5); y5l:=good.y y5l; x5l:=good.x x5l;
 forsuffixes e=l,r: path p.e; p.e=z4e{right}..tension .9 and 1..z5e;
  if angle direction 1 of p.e>75:
   p.e:=z4e{right}..tension atleast.9 and 1..{dir 75}z5e; fi endfor
 make_paths2(rev.l) pulled_super_arc.e(2,3)(.7superpull)
  & pulled_super_arc.e(3,4)(.5superpull) & p.e; % arc and lower terminal
  fill lpath2 & p1 & rpath2--cycle;
fi
penlabels(0,1,2,3,4,5); endchar;

cmchar "The letter d";
beginchar("d",10u#+serif_fit#,asc_height#,0);
italcorr asc_height#*slant-serif_fit#+.5stem#-2u#;
adjust_fit(0,serif_fit#);
path p[];
pos1(stem',0); pos2(stem,0);
pos0'(stem',0); pos0(stem,0); z0r=z0'r; x0'=x1; x0=x2;
x1r=w-jut; y1=h;
numeric edge; edge=x2l;
pos3(if hefty:thin_join else: hair fi,0);
pos4(vair,90); pos5(curve,180); pos6(vair,270); penpos7(x3r-x3l,360);
x3l=min(x3l-(x3r-x2r),1/3[x2,edge]);
y3=1/8[bar_height,x_height];
x4l=.5(w-serif_fit)-.3u; y4r=x_height+oo;
x5r=0; y5=.5x_height;
x6l=x4l-.2u; y6r=-oo;
x7=x3; y7=min(y3,y6+y4-y3+.6vair);
(x,y4r)=whatever[z3l,z4l]; x4r:=max(x,.5[x5r,x4]);
(x',y6r)=whatever[z7l,z6l]; x6r:=max(x',.5[x5r,x6]);
make_paths1(rev.l) z3e{up}...pulled_arc.e(4,5); % bowl top
make_paths2(rev.l) pulled_arc.e(5,6)...{up}z7e; % bowl bottom
y0=ypart(((edge,h)--(edge,0))intersectionpoint(z3l{up}...{left}z4l));
y2=if serifs:-min(oo,serif_drop) else: 0 fi;
if serifs:
 p1=sloped_serif_path.l(1,0',a,1/3,jut,serif_drop);  % upper serif
 p2=sloped_serif_path.r(2,0,b,1/3,jut,min(oo,serif_drop)); % lower serif
else:
 p1=pen_rlend1(up,tiny,down);
 p2=pen_lrend2(down,tiny,up);
fi
fill combine(p1--z0'l,rpath1) & combine(rpath2,z1l--p2)--cycle;
if x3l<=x2l:
 %unfill lpath2 & lpath1--cycle;
else:
 p3=(x2l,h)--z2l;
 unfill combine(lpath1,combine(p3,lpath2))--cycle;
fi
penlabels(0,1,2,3,4,5,6,7); endchar;

cmchar "The letter e";
beginchar("e",7.25u#+max(.75u#,.5curve#),x_height#,0);
italcorr .5[bar_height#,x_height#]*slant+.5min(curve#-1.5u#,0);
adjust_fit(if monospace: .25u#,.5u# else: 0,0 fi);
numeric left_curve,right_curve;
left_curve=right_curve+6stem_corr=curve if not serifs: -3stem_corr fi;
if right_curve<tiny.breadth: right_curve:=tiny.breadth; fi
if left_curve<tiny.breadth: left_curve:=tiny.breadth; fi
pickup tiny.nib; pos1(right_curve,0);
pos2(vair,90); pos3(left_curve,180);
y1=good.y bar_height; top y2r=h+vround 1.5oo; y0l=bot y1;
rt x1r=hround min(w,w-.5u+.5right_curve);
lft x3r=0; x2=.5w+.25u;
{{interim superness:=more_super;
 filldraw stroke super_arc.e(1,2)}};  % right bowl
y3=.5[y2,y4]; bot y4r=-oo; x4=x2+.25u;
if serifs: pos4(vair',270); pos5(hair,360);
 y5=max(good.y(.5bar_height-.9),y4l+vair); x5r=x1r;
 (x,y4l)=whatever[z4r,z5]; x4l:=min(x,x4l+.5u);
 filldraw stroke pulled_arc.e(2,3) & pulled_arc.e(3,4)
  ...{x5-x4,5(y5-y4)}z5e; % left bowl, arc, and terminal
else: pos4(vair,270);
 filldraw stroke super_arc.e(2,3) & super_arc.e(3,4);  % left bowl and arc
 pickup fine.nib; pos4'(vair,270); z4=z4';
 pos5(.5[vair,flare],275); rt x5r=w;
 y5r=good.y(y5r+1/3bar_height-y5); y5l:=good.y y5l; x5l:=good.x x5l;
 filldraw stroke term.e(4',5,right,1,4); fi  % terminal
path testpath; testpath=super_arc.r(2,3) & super_arc.r(3,4);
y1'r=y0r=y0l+.6[thin_join,vair]; y1'l=y0l; x1'l=x1'r=x1;
forsuffixes $=l,r:
 x0$=xpart(((0,y0$)--(x1,y0$)) intersectionpoint testpath); endfor
fill stroke z0e--z1'e;  % crossbar
penlabels(0,1,2,3,4,5); endchar;

cmchar "The letter f";
beginchar("f",5.5u#,asc_height#,0);
italcorr asc_height#*slant+if serifs:flare#-.25u# else: 1.25u# fi;
adjust_fit(stem_shift#,if monospace: .5u# else:-stem_shift# fi);
path p[];
pos1(stem',0); x1l=jut;
numeric bulb_diam;
if serifs: bulb_diam=hround .8[stem,flare];
 pos2(bulb_diam,0); y2+.5bulb_diam=.9[x_height,h+oo];
 x2r=w;
else: pos2(5/7[vair,flare],90); y2r=h;
 x2=hround(if monospace:w-.5u else:w fi); fi
y3r=y4r=x_height; x3=hround .5u-1;
pos3(bar,90); pos4(bar,90);
x4=x2;
forsuffixes $=r,,l:
 x5$=x6$=x1$;
 y5$=y3r; y6$=y3l;
endfor
p1=f_stroke_path(1,2,a);
if serifs:
  p2=dish_serif_path(1,5,b,1/3,jut,c,1/3,if not monospace:1.25fi\\ jut);
else:
  p2=pen_lrend1(down,tiny,up);
fi
fill p1--z5l--pen_rlend3(left,crisp,right)--z6l--p2
  --z6r--pen_lrend4(right,crisp,left)--z5r--cycle;
penlabels(1,2,3,4,5,6); endchar;

iff not variant_g: cmchar "The letter g";
beginchar("g",9u#,x_height#,desc_depth#);
italcorr x_height#*slant+.25u#;
adjust_fit(0,0);
path p[];
numeric light_vair,light_curve,loop_top,loop_side;
light_vair=Vround(.5[thin_join,vair]+vair_corr);
light_curve=max(fine.breadth,hround(curve-3stem_corr));
loop_top=if serifs: Vround .77[vair,fudged.stem] else: vair fi;
loop_side=hround .64[vair,fudged.stem];
pos1(light_vair,90);
pos2(light_curve,180); pos3(light_vair,270);
pos4(light_curve,360); pos11(loop_top,90);
y11r=good.y(y11r+.15bar_height-y11); x1=x3=.5[x2,x4]; y2=y4=.5[y1,y3];
x2r=hround(.5u); x4r=w-hround(2.5u-.5light_curve);
y1r=h+oo; y3r=good.y(y3r+.27[y11r,y1l]-y3);
x0=superness[x1,x4]; y0=superness[y4,y1];  % NE point on the super bowl
x8'=superness[x3r,x2r]; y8'=superness[y2r,y3r];  % SW point on the super bowl
x8''=superness[x3,x2]; y8''=superness[y2,y3];
pos0(thin_join,angle(z0-z8')+90);
pos8(thin_join,angle(z0-z8')-90); z8=.618[z8',z8''];
if serifs: pos5(vair,90);
 pos6(hair,0); pos7(.5[hair,flare],0); x5=.75[x0,x6]; y5r=h+o;
 x6r=w; y6+(.5[hair,flare])/2=.95[bar_height,h]+oo;
 filldraw stroke z0e..{right}z5e; p1=bulb_path(5,6,7,right);  % ear
 fill p1--cycle;
else: pos5(vair',100); y5r=h+oo;
 x5l=hround(w-.25u); y5l:=good.y y5l;
 filldraw z0l{z5-z0}..z5l--z5r{left}..{curl 1}z0r--cycle; fi  % ear
pos10(loop_top,90); x10=x8+.75u; y10=y11;
pos12(loop_side,0); pos13(light_vair,-90);
pos14(loop_side,-180); pos10'(.5[thin_join,vair],-270);
x12r=w-hround(.5u); y12=y14=.5[y11,y13];
x11=x13=max(.5w,x10+eps); y13r=-d-oo-1; x14r=0; z10'l=z10l;
make_paths1(rev.l) pulled_arc.e(1,2) & pulled_arc.e(2,3);  % left half of bowl
make_paths2(rev.l) pulled_arc.e(3,4) & pulled_arc.e(4,1);  % right half of bowl
make_paths3(rev.r) z8e{z8-z0}..z10e---z11e;  % link
make_paths4(rev.r) {{interim superness:=hein_super; super_arc.e(11,12)}}
 & super_arc.e(12,13); % right of loop
make_paths5(rev.r) super_arc.e(13,14) & super_arc.e(14,10');  % left of loop
fill rpath2 & combine(combine(rpath1, lpath3), rpath5) & rpath4
 & combine(rpath3, rpath1) & cycle;
unfill lpath1 & lpath2 & cycle;
if y10l>=y10'l:
 unfill lpath4 & lpath5--cycle;
else:
 unfill lpath4 & combine(lpath5, lpath3) & cycle;
fi
penlabels(0,1,2,3,4,5,6,7,8,10,11,12,13,14); labels(8',8''); endchar;

iff variant_g: cmchar "Variant letter g";
beginchar("g",10u#+serif_fit#,x_height#,desc_depth#);
italcorr x_height#*slant-serif_fit#+.5stem#-2u# if serifs:+.5u# fi;
adjust_fit(0,serif_fit# if serifs: -.5u# fi);
pickup tiny.nib; pos1(stem',0); pos2(stem,0);
pos0'(stem',0); pos0(stem,0); z0r=z0'r; x0'=x1; x0=x2;
rt x1r=hround(w-2.5u+.5stem');
numeric edge; edge=lft x2l;
path edge_path; edge_path=(edge,h)--(edge,0);
pickup fine.nib; pos3(if hefty:thin_join else: hair fi,0);
pos4(vair,90); pos5(curve,180); pos6(vair,270); penpos7(x3r-x3l,360);
lft x3l=min(lft x3l-(rt x3r-tiny.rt x2r),2/3[lft x2,edge]); y3=bar_height;
x4l=.5(w-serif_fit)-.3u; top y4r=x_height+oo;
lft x5r=hround max(1.35u-.5curve,.6u); y5=.5x_height;
x6l=x4l-.2u; bot y6r=vround 1/3vair;
lft x7l=edge; y7=min(y3,y6+y4-y3+.6vair);
(x,y4r)=whatever[z3l,z4l]; x4r:=max(x,.5[x5r,x4]);
(x',y6r)=whatever[z7l,z6l]; x6r:=max(x',.5[x5r,x6]);
filldraw stroke z3e{up}...{left}z4e&super_arc.e(4,5)
 &super_arc.e(5,6)&z6e{right}...{up}z7e;  % bowl
y1=ypart(edge_path intersectionpoint(z3l{up}...{left}z4l));
y0=ypart(edge_path intersectionpoint(z7l{down}...{left}z6l));
pickup tiny.nib; bot y2=if serifs: -.25d else: 0 fi;
filldraw stroke z1e--z0'e--z0e--z2e;  % stem
pickup crisp.nib;
pos8(hround(hair-stem_corr),0); pos7'(stem',0);
z7'=z1; x8r=x7'r; top y8=h+oo;
filldraw stroke z7'e--z8e;  % point
if serifs: pickup tiny.nib;
 pos9(vair,-90); x9=.5[x2,x10]; bot y9r=-d-o-1;
 pos10(hair,-180); lft x10r=hround u; y10=-.75d+.5flare;
 pos11(flare,-180); z11r=z10r;
 bulb(9,10,11); filldraw stroke super_arc.e(2,9);  % tail
else: pickup fine.nib; pos2'(stem,0); z2'=z2;
 z2''r=z2'r; z2''=z2'; z2''l=(x2'l,0);
 pos9(vair,-90); x9=4.5u; bot y9r=-d-o-1;
 pos10(.5[vair,flare],-90); lft x10=hround 1.25u;
 y10r=good.y -5/6d; y10l:=good.y y10l;
 filldraw stroke z2'e..z2''e&super_arc.e(2'',9)
  & term.e(9,10,left,.9,4); fi  % tail
penlabels(0,1,2,3,4,5,6,7,8,9,10,11); endchar;

cmchar "The letter h";
beginchar("h",10u#,asc_height#,0);
italcorr .5[bar_height#,x_height#]*slant-serif_fit#+.5stem#-2u#;
adjust_fit(serif_fit#+stem_shift#,serif_fit#-stem_shift#);
path p[];
pos1(stem,0); pos2(stem,0);
pos1'(stem',0); pos2'(stem',0); pos3(stem,0);
x1l=jut; x1l=x1'l=x2l=x2'l; x3r=w-jut;
y1=h; y2=0; y1=y1'; y2=y2';
h_stroke_path1(2,a,3,4);  % arch and right stem
if serifs:
 numeric inner_jut;
 if x2r+jut+.5u+1<=x4l-jut: inner_jut=jut;
 else: x2r+jut+.5u+1=x4l-inner_jut; fi
 p1=dish_serif_path(2,1,c,1/3,jut,d,1/3,jut); % lower left serif
 p2=sloped_serif_path.l(1',2',b,1/3,jut,serif_drop);
 p3=dish_serif_path(4,3,e,1/3,inner_jut,f,1/3,jut); % lower right serif
else:
 p1=pen_lrend1(down,tiny,up);
 p2=pen_rlend2'(up,tiny,down);
 p3=pen_lrend3(down,tiny,up);
fi
fill p1--lpath1--p3--combine(rpath1,z2'r--p2)--cycle;
penlabels(1,2,3,4); endchar;

cmchar "The letter i";
numeric dot_diam#; dot_diam#=max(dot_size#,cap_curve#);
beginchar("i",5u#,min(asc_height#,10/7x_height#+.5dot_diam#),0);
define_whole_blacker_pixels(dot_diam);
italcorr h#*slant-serif_fit# + .5dot_diam# -2u#;
adjust_fit(serif_fit#+stem_shift# if monospace:+.25u#fi,serif_fit#-stem_shift#);
pos1(stem',0); pos2(stem',0);
x1l=jut; x1=x2;
y1=x_height if serifs: +min(oo,serif_drop) fi; y2=0;
pos3(dot_diam,0); pos4(dot_diam,90);
if serifs: x3r=max(x1r,hround(x1+.5dot_diam-.2jut)-.5tiny)
else: x3=x1-.5 fi;
y4r=h+1;
if y4l-y1<slab: y4l:=min(y4r-eps,y1+tiny+slab); fi
x3=x4; y3=.5[y4l,y4r]; dot_path(3,4);  % dot
if serifs:
    fill sloped_serif_path.l(1,2,a,1/3,.95jut,serif_drop)--
        dish_serif_path(2,1,b,1/3,x2l,c,1/3,w-x2r)--cycle;
else:
    fill pen_rlend1(up,tiny,down)--pen_lrend(down,tiny,up)--cycle;
fi
penlabels(1,2,3,4); endchar;

cmchar "The letter j";
numeric dot_diam#; dot_diam#=max(dot_size#,cap_curve#);
beginchar("j",5.5u#,min(asc_height#,10/7x_height#+.5dot_diam#),desc_depth#);
define_whole_blacker_pixels(dot_diam);
italcorr h#*slant-serif_fit#+.5stem#-2u#;
adjust_fit(serif_fit#+2stem_shift# if monospace:+\\.5u# fi,
 serif_fit#-2stem_shift# if monospace:-.5u# fi);
pos1(stem',0); pos2(stem',0);
x1r=w; x1=x2;
y1=x_height if serifs: +min(oo,serif_drop) fi; y2=-1/3d;
pos3(dot_diam,0); pos4(dot_diam,90);
x3r=x1r; y4r=h+1;
if y4l-y1<slab: y4l:=min(y4r-eps,y1+tiny+slab); fi
x3=x4; y3=.5[y4l,y4r]; dot_path(3,4);  % dot
if serifs:
 pos5(vair,-90); pos6(hair,-180); pos7(flare,-180);
 x5=.5[x2,x6r]; y5r=-d-oo; y6-.5flare=-.88d;
 if monospace: x6r=0 else: z6r=z7r; x7r=0 fi;
 (x,y5r)=whatever[z5l,z2l]; x5r:=max(x,.5[x6r,x5]);
 make_paths1(rev.r) z2e{down}...z5e{left};
 fill sloped_serif_path.l(1,2,a,1/3,1.1jut,serif_drop)--
     lpath1 & bulb_path(5,6,7,left) & rpath1--cycle;
else:
 filldraw stroke z1e--z2e;  % stem
 pickup fine.nib; pos2'(stem',0); z2'=z2;
 pos6(.2[vair,stem'],-90); pos7(vair,-90);
 x7r=hround -.75u; y7r=vround 5/6(-d-oo);
 (x,y7l)=whatever[z7r,z3]; x7l:=x;
 z5r=z2'r; (x2'l,y5l)=whatever[z7l,z5r]; x5l=x2'l; y5=y5r;
 x6r=.5[x7r,x5r]; x6l:=.5[x7l,x5l]; y6r=-d-oo;
 filldraw stroke z2'e..{down}z5e & super_arc.e(5,6)
  & z6e{left}..z7e; fi  % arc and terminal
penlabels(1,2,3,4,5,6,7); endchar;

cmchar "The letter k";
beginchar("k",9.5u#,asc_height#,0);
italcorr x_height#*slant-.2u#;
adjust_fit(serif_fit#,serif_fit#);
numeric right_jut,stem[],alpha[];
path p[];
stem1=max(tiny.breadth,hround(fudged.stem-stem_corr));
stem2=max(tiny.breadth,hround(fudged.stem-2stem_corr));
stem3=max(tiny.breadth,hround(fudged.hair if hefty:-\\4stem_corr fi));
stem4=max(tiny.breadth,hround(fudged.stem-3stem_corr));
if serifs: right_jut=.6jut; else: right_jut=.4tiny; fi
pos1(stem1,0); pos2(stem2,0); y1=h; y2=0;
x1l=x2l=jut;
y3=x_height; x3r=x6r-hround(.4u);
y6=0; x6r=w-right_jut;
x4=x11=x1; y4=.7bar_height; y11=y3;
alpha1=diag_ratio(1,.5(stem3-tiny),y3-y4,x3r-x4);
alpha2=diag_ratio(1,.5(stem4-tiny),y11-y6,x6r-x1);
penpos3(alpha1*(stem3-tiny),0); penpos4(whatever,-90);
z5=.5[z5l,z5r]; penpos6(alpha2*(stem4-tiny),0);
forsuffixes $=l,r: y3'$=x_height; y6'$=0; z4$=z3'$+whatever*(z3-z4);
 z5$=z6'$+whatever*(z11-z6)=whatever[z3,z4]; endfor
z3'r=z3r+whatever*(z3-z4);
% we have also |z3'l=z3l+penoffset z4-z3 of currentpen+whatever*(z3-z4)|;\]
z6'r=z6r+whatever*(z11-z6);
z6'l=z6l+whatever*(z11-z6);
pos0(stem1,0); pos0'(stem2,0); y0=y0'; x0l=x1l; x0'=x2;
z0r=whatever[z3,z4];
if serifs: numeric inner_jut;
 if x2r+jut+.5u+1<=x6l-jut: inner_jut=jut;
 else: x2r+inner_jut+.5u+1=x6l-inner_jut; fi
 p1=sloped_serif_path.l(1,0,a,1/3,jut,serif_drop); % upper stem serif
 p2=dish_serif_path(2,0',b,1/3,jut,c,1/3,inner_jut);  % lower stem serif
 p3=dish_serif_path(3,4,d,2/3,1.4jut,e,1/2,right_jut)(dark); % upper diagonal
 p4=dish_serif_path(6,5,f,1/2,inner_jut,g,1/3,right_jut)(dark); % lower diagonal
else:
 p1=pen_rlend(up,tiny,down);
 p2=pen_lrend(down,tiny,up);
 p3=diag_end(4r,3'r,1,.5,3'l,4l); % upper diagonal
 p4=diag_end(5l,6'l,.5,1,6'r,5r);  % lower diagonal
fi
% Calculate intersection points of the diagonals
z21=whatever[z1r,z0r]=whatever[z3'l,z4l]; % upper diagonal and stem, top
z22=whatever[z0'r,z2r]=whatever[z4r,z3'r]; % upper diagonal and stem, bottom
z23=whatever[z4r,z3'r]=whatever[z5l,z6'l]; % diagonals, bottom
z24=whatever[z6'r,z5r]=whatever[z4r,z3'r]; % diagonals, top
fill p2--z22--z23--p4--z24--p3--z21--p1--cycle;
penlabels(0,1,2,3,4,5,6,11); endchar;

cmchar "The letter l";
beginchar("l",5u#,asc_height#,0); l_width#:=5u#+2serif_fit#;
italcorr asc_height#*slant-serif_fit#+.5stem#-2u#;
adjust_fit(serif_fit#+stem_shift#,serif_fit#-stem_shift#);
pos1(stem',0); pos2(stem',0);
x1l=hround(.5w-.5stem'); x1=x2; y1=h; y2=0;
if serifs:
    fill sloped_serif_path.l(1,2,a,1/3,x1l,serif_drop)--
        dish_serif_path(2,1,b,1/3,x2l,c,1/3,w-x2r)--cycle;
else:
    fill pen_rlend1(up,tiny,down)--pen_lrend(down,tiny,up)--cycle;
fi
penlabels(1,2); endchar;

cmchar "The letter m";
beginchar("m",15u#,x_height#,0);
italcorr .5[bar_height#,x_height#]*slant-serif_fit#+.5stem#-2u#;
adjust_fit(serif_fit#+stem_shift#,serif_fit#-stem_shift#);
path p[];
numeric shaved_stem; shaved_stem=hround(mfudged.stem-2stem_corr);
pos1(mfudged.stem,0); pos2(mfudged.stem,0);
pos1'(shaved_stem,0); pos2'(shaved_stem,0);
pos3(mfudged.stem,0); pos5(mfudged.stem,0);
x1l=jut; x1l=x1'l=x2l=x2'l; % stem, sic
x5r=w-jut; x5-x3=x3-x1;
y1=h+min(oo,serif_drop); y2=0; y1=y1'; y2=y2';
h_stroke_path1(2,a,3,4);  % left arch and middle stem
h_stroke_path2(4,b,5,6);  % right arch and right stem
if serifs:
 p1=sloped_serif_path.l(1',2',c,1/3,jut,serif_drop); % upper left serif
 numeric inner_jut;
 if rt x2r+jut+.5u+1<=lft x4l-jut: inner_jut=jut;
 else: rt x2r+jut+.5u+1=lft x4l-inner_jut; fi
 p2=dish_serif_path(2,1,d,1/3,jut,e,1/3,jut); % lower left serif
 p3=dish_serif_path(4,3,f,1/3,inner_jut,g,1/3,jut); % lower middle serif
 p4=dish_serif_path(6,5,h,1/3,inner_jut,i,1/3,jut); % lower right serif
else:
 p1=pen_rlend(up,tiny,down);
 p2=pen_lrend(down,tiny,up);
 p3=pen_lrend(down,tiny,up);
 p3=pen_lrend(down,tiny,up);
fi
fill p2--lpath1--p3--lpath2--p4--combine(combine(rpath2,rpath1),z2'r--p1)
    --cycle;
penlabels(1,2,3,4,5,6); endchar;

cmchar "The letter n";
beginchar("n",10u#,x_height#,0);
italcorr .5[bar_height#,x_height#]*slant-serif_fit#+.5stem#-2u#;
adjust_fit(serif_fit#+stem_shift#,serif_fit#-stem_shift#);
path p[];
pos1(stem,0); pos2(stem,0);
numeric shaved_stem; shaved_stem=hround(stem-2stem_corr);
pos1'(shaved_stem,0); pos2'(shaved_stem,0); pos3(stem,0);
x1l=jut; x1l=x1'l=x2l=x2'l; x3r=w-jut;
y1=h+min(oo,serif_drop); y2=0; y1=y1'; y2=y2';
h_stroke_path1(2,a,3,4);
if serifs:
 numeric inner_jut;
 if x2r+jut+.5u+1<=x4l-jut: inner_jut=jut;
 else: x2r+jut+.5u+1=x4l-inner_jut; fi
 p1=dish_serif_path(2,1,c,1/3,jut,d,1/3,jut); % lower left serif
 p2=sloped_serif_path.l(1',2',b,1/3,jut,serif_drop);
 p3=dish_serif_path(4,3,e,1/3,inner_jut,f,1/3,jut); % lower right serif
else:
 p1=pen_lrend1(down,tiny,up);
 p2=pen_rlend2'(up,tiny,down);
 p3=pen_lrend3(down,tiny,up);
fi
fill p1--lpath1--p3--combine(rpath1,z2'r--p2)--cycle;
penlabels(1,2,3,4); endchar;

cmchar "The letter o";
beginchar("o",9u#,x_height#,0);
italcorr .7x_height#*slant;
adjust_fit(if monospace: .5u#,.5u# else: 0,0 fi);
penpos1(vair,90); penpos3(vair',-90);
penpos2(curve,180); penpos4(curve,0);
x2r=0;
x4r=w-x2r; x1=x3=.5w; y1r=h+vround 1.5oo; y3r=-oo;
y2=y4=.5h-vair_corr; y2l:=y4l:=.52h;
make_paths1(rev.l) pulled_arc.e(1,2) & pulled_arc.e(2,3)
 & pulled_arc.e(3,4) & pulled_arc.e(4,1) & cycle;  % bowl
fill lpath1;
unfill rpath1;
penlabels(1,2,3,4); endchar;

cmchar "The letter p";
beginchar("p",10u#+serif_fit#,x_height#,desc_depth#);
italcorr .5x_height#*slant+min(.5curve#-.85u#,-.1u#);
adjust_fit(serif_fit#,0);
pos1(stem',0); pos2(stem,0);
pos0'(stem',0); pos0(stem,0); z0l=z0'l; x0'=x1; x0=x2;
x1l=jut; y1=h if serifs: +min(oo,serif_drop) fi;
path p[];
numeric edge; edge=x2r;
pos3(if hefty:thin_join else: hair fi,180);
pos4(vair',90); pos5(curve,0); pos6(vair,-90); penpos7(x3l-x3r,-180);
x3l=max(x3l-(x3r-x2l), 1/3[x2,edge]);
y3=1/8[bar_height,x_height];
x4l=w-.5(w-serif_fit)+.5u; y4r=x_height+oo;
x5r=w; y5=.5x_height;
x6l=x4l-.2u; y6r=-oo;
x7=x3; y7=min(y3,y6+y4-y3+.6vair);
(x,y4r)=whatever[z3l,z4l]; x4r:=min(x,.5[x5r,x4]);
(x',y6r)=whatever[z7l,z6l]; x6r:=min(x',.5[x5r,x6]);
make_paths1(rev.r) z3e{up}...{right}z4e&super_arc.e(4,5);
make_paths2(rev.r) super_arc.e(5,6)&z6e{left}...{up}z7e;  % bowl
y0=ypart(((edge,h)--(edge,0))intersectionpoint(z3l{up}...{right}z4l));
y2=-d;
% TODO: The next few lines expand the stem in certain cases. This is not
% implemented.
%pickup crisp.nib; pos8(hair,0); pos7'(stem,0);
%z7'=z2; x8l=x7'l; y8=0;
%filldraw stroke z7'e--z8e;  % point
if serifs:
 p1=sloped_serif_path.l(1,0',a,1/3,jut,serif_drop);  % upper serif
 p2=dish_serif_path(2,0,b,1/3,jut,c,1/3,jut); % lower serif
else:
 p1=pen_rlend1(up,tiny,down);
 p2=pen_lrend(down,tiny,up);
fi
fill combine(p2--z0r,rpath2) & combine(rpath1,z0'r--p1)--cycle;
if x3l>=x0'r:
 unfill lpath1 & lpath2--cycle;
else:
 unfill combine(combine(lpath2,z2r--z0r),lpath1) & cycle;
fi
penlabels(0,1,2,3,4,5,6,7,8); endchar;

cmchar "The letter q";
beginchar("q",10u#+serif_fit#,x_height#,desc_depth#);
italcorr x_height#*slant-serif_fit#+.5stem#-2u# if serifs:+.5u# fi;
adjust_fit(0,serif_fit# if serifs: -.5u# fi);
pickup tiny.nib; pos1(stem',0); pos2(stem,0);
pos0'(stem',0); pos0(stem,0); z0r=z0'r; x0'=x1; x0=x2;
rt x1r=w-jut;
numeric edge; edge=lft x2l;
pickup fine.nib; pos3(if hefty:thin_join else: hair fi,0);
pos4(vair',90); pos5(curve,180); pos6(vair,270); penpos7(x3r-x3l,360);
lft x3l=min(lft x3l-(rt x3r-tiny.rt x2r),2/3[lft x2,edge]); y3=bar_height;
x4l=.5(w-serif_fit)-.3u; top y4r=x_height+oo;
lft x5r=0; y5=.5x_height;
x6l=x4l-.2u; bot y6r=-oo; y7=min(y3,y6+y4-y3+.6vair);
lft x7l=min(lft x7l-(rt x7r-tiny.rt x2r),1/3[lft x2,edge]);
(x,y4r)=whatever[z3l,z4l]; x4r:=max(x,.5[x5r,x4]);
(x',y6r)=whatever[z7l,z6l]; x6r:=max(x',.5[x5r,x6]);
filldraw stroke z3e{up}...{left}z4e&super_arc.e(4,5)
 &super_arc.e(5,6)&z6e{right}...{up}z7e;  % bowl
y1=ypart(((edge,h)--(edge,0))intersectionpoint(z3l{up}...{left}z4l));
y0=ypart(((edge,h)--(edge,0))intersectionpoint(z6l{right}...{up}z7l));
pickup tiny.nib; bot y2=-d;
filldraw stroke z1e--z0'e--z0e--z2e;  % stem
pickup crisp.nib;
pos8(hround(hair-stem_corr),0); pos7'(stem',0);
z7'=z1; x8r=x7'r; top y8=h+oo;
filldraw stroke z7'e--z8e;  % point
if serifs: dish_serif(2,0,b,1/3,jut,c,1/3,jut); fi  % lower serif
penlabels(0,1,2,3,4,5,6,7,8); endchar;

cmchar "The letter r";
numeric r_flare#; r_flare#=.75[if serifs: stem# else: vair# fi,flare#];
define_whole_blacker_pixels(r_flare);
beginchar("r",if serifs:max(7u#,5.5u#+r_flare#) else:6.5u# fi,x_height#,0);
italcorr x_height#*slant if not serifs: +.25u# fi;
adjust_fit(serif_fit#,0);
path p[];
% pickup fine.nib;
y4r=h+oo;
if serifs: pos4(vair,90); pos5(hair,0);
 x4=w-max(1.75u,.25u+r_flare);
 x5r=w; y5+.5r_flare=.9[bar_height,h]+oo;
 pos6(r_flare,0);
 % bulb(4,5,6);  % bulb
else:
 pos4(r_flare,90); x4=hround(w-.25u);
fi
pos3(thin_join,180); x3l+fine=x2r+tiny; y3=bar_height;
% pickup tiny.nib;
pos0(stem',0); pos2(stem',0);
pos1(hround(stem-3stem_corr),0); y1=h+min(oo,serif_drop);
pos0'(hround(stem-3stem_corr),0); y0=y0'=y3; x1l=x0l=x0'l=x2l;
x1l=jut; y2=0;
%pickup fine.nib;
make_paths1(rev.r) z3e{up}...{right}z4e;  % arc
%pickup tiny.nib;
if serifs:
 p1=z2r--sloped_serif_path.l(1,0',a,1/3,jut,serif_drop);  % upper serif
 p2=dish_serif_path(2,0,b,1/3,jut,c,1/3,1.25jut)--z1r; % lower serif
 p3=bulb_path(4,5,6,right);
else:
 p1=z2r--pen_rlend1(up,tiny,down);
 p2=pen_lrend2(down,tiny,up)--z1r;
 p3=z4l--z4r; % FIXME: should use stroke_end or something
fi
fill combine(p2, lpath1) & p3 &
    combine(rpath1, p1) -- cycle;
penlabels(1,2,3,4,5,6); endchar;

cmchar "The letter s";
beginchar("s",7.1u#,x_height#,0);
italcorr x_height#*slant-if serifs:.55u# else:.3u# fi;
adjust_fit(if monospace: .5u#,.4u# else: 0,if serifs:0 else:-.2u# fi fi);
numeric theta; theta=90-angle(40u,h); slope:=-h/40u;  % angle at middle
numeric s_slab; s_slab=if serifs:vair else:Vround .1[vair,stem] fi;
numeric ess'; ess'=max(fine.breadth,ess);
pickup fine.nib; pos2(max(fine.breadth,s_slab-vround vair_corr),-100);
pos0(ess',theta); pos7(s_slab,-90); x2l=x0=x7=.5w;
top y2l=h+vround 1.5oo; bot y7r=-oo;
y0-.5ess'=y7l+if serifs:.54 else: .52 fi\\(y2r-y7l-ess');
lft x3l=0; rt x6r=w;
x3r-x3l=x6r-x6l=hround .5[s_slab,ess']-fine;
ellipse_set(2l,3l,4l,0l); ellipse_set(2r,3r,4r,0r); y3=y3r;
ellipse_set(7l,6l,5l,0l); ellipse_set(7r,6r,5r,0r); y6=y6r;
interim superness:=more_super;
filldraw stroke super_arc.e(2,3) & z3e{down}
 ..z4e---z5e..z6e{down} & super_arc.e(6,7);  % main stroke
if serifs: pos1(hair,180); pos8(hair,180);
 x1l=x6r-hround(.45u); lft x8r=0;
 bot y1=min(bot y2r,vround 1/5[top y5r,h]);
 top y8=max(top y7l,vround 5/6 bot y4l);
 filldraw stroke z1e{up}....{left}z2e;  % upper arc
 filldraw stroke z7e{left}....{x8-x7,5(y8-y7)}z8e;  % lower arc
 path upper_arc, lower_arc;
 upper_arc=z1{up}....{left}z2; lower_arc=z7{left}....{x8-x7,5(y8-y7)}z8;
 pos10(.3[fine.breadth,cap_hair],0); pos9(.3[fine.breadth,cap_hair],0);
 x10r=x1l; top y10=top y2l; x9l=x8r; bot y9=bot y7r;
 x1l-x1'=x8'-x8r=1.6cap_curve-fine; y1'=y1; y8'=y8;
 numeric t; t=xpart(upper_arc intersectiontimes(z10l--z1'));
 filldraw z1l--z10r--z10l--subpath(t,0) of upper_arc--cycle;  % upper barb
 t:=xpart(lower_arc intersectiontimes(z9r--z8'));
 filldraw z8r--z9l--z9r--subpath(t,1) of lower_arc--cycle;  % lower barb
else: pos1(4/7[s_slab,flare],-100); pos8(flare,-100);
 x1l=good.x(x1l+w-u-rt x1); lft x8r=hround .5u;
 top y1l=vround(.93h+1.5oo); bot y8r=vround .1h-oo;
 filldraw stroke term.e(2,1,right,.9,4);  % upper arc and terminal
 filldraw stroke term.e(7,8,left,1,4); fi  % lower arc and terminal
penlabels(0,1,1',2,3,4,5,6,7,8,8',9,10); endchar;

cmchar "The letter t";
beginchar("t",6u#+max(u#,.5stem#),
 min(asc_height#,if hefty:9/7 else:10/7 fi\\ x_height#),0);
italcorr x_height#*slant if serifs: -.9u# else: -.4u# fi;
adjust_fit(0,if serifs: 0 else: -.5u# fi);
numeric shaved_stem; shaved_stem=hround(stem if hefty:-\\2stem_corr fi);
pickup fine.nib; pos2(shaved_stem,180);pos3(shaved_stem,180);
lft x2r=lft x3r=hround(2.5u-.5shaved_stem);
y2=y8; y3=max(.5bar_height,2vair);
pickup crisp.nib; pos8(bar,90);
rt x8=hround(w-.3u); top y8r=x_height; lft x7=0; y7l=y8l;
if hefty: pos7(bar,90);
 filldraw stroke z7e--z8e;  % crossbar
 pickup tiny.nib; pos1(hround(shaved_stem-stem_corr),0);
 rt x1r=fine.rt x2l; top y1=h;
 penpos2'(x1r-x1l,0); x2'=x1; y2'=y2;
 filldraw stroke z1e--z2'e;  % upper terminal
else: pos7(vair,90); pos1(hair,0);
 rt x1r=fine.rt x2l; top y1=h;
 filldraw z1l{down}...{left}z7r--z7l--z8l
  --z8r--(x1r,y8r)--z1r--cycle; fi  % upper terminal and crossbar
pickup fine.nib; interim superness:=more_super;
pos4(vair',-90); bot y4r=-oo; rt x5r=w;
if serifs: pos5(hair,0); y5=y3; x4l=.5[x3l,x5l];
 (x,y4r)=whatever[z4l,z5l]; x4r:=max(x,.5[x3r,x4]);
 filldraw stroke z2e..super_arc.e(3,4)...{up}z5e;  % stem and hook
 pickup crisp.nib; pos6(hair,0); pos5'(hair,0);
 x6=x5=x5'; top y6=max(vround .75bar_height,top y5); y5=y5';
 filldraw stroke z5'e--z6e;  % terminal
else: pos5(vair,-75); top y5l=vround .2[top y4l,bar_height];
 x5l:=good.x x5l; x4l=1/3[x3l,x5l]; x4r:=1/3[x3r,x5r]; y3l:=y3l+.2vair;
 filldraw stroke z2e..super_arc.e(3,4);  % stem and hook
 path p; p=stroke z4e{right}..tension .9 and atleast 1..z5e;  % terminal
 if (xpart(z5l-precontrol 1 of p)<0) or (xpart(z5r-postcontrol 2 of p)<0):
  filldraw stroke z4e{right}...{up}z5e;
 else: filldraw p; fi fi;
penlabels(1,2,3,4,5,6,7,8); endchar;

cmchar "The letter u";
beginchar("u",10u#,x_height#,0);
italcorr x_height#*slant-serif_fit#+.5stem#-2u#;
adjust_fit(serif_fit#+stem_shift#,serif_fit#-stem_shift#);
path p[];
numeric light_vair; light_vair=vair if hefty: -vround 2vair_corr fi;
if light_vair<fine.breadth: light_vair:=fine.breadth; fi
pos1(stem,0); pos2(stem,0);
pos3(stem,0); pos4(stem',0);
x1l=jut; x1=x2; x3r=w-jut; x3r=x4r;
if serifs: y1=h+min(oo,serif_drop); y4=-min(oo,serif_drop);
else: y1=h; y4=0; fi
pos0(stem,0); pos0'(stem',0); x0=x3; x0'=x4; y0=y0';
penpos2'(stem-fine,-180); z2'=z2; y3=y1; y2=.5bar_height;
penpos5(max(eps,light_vair-fine),-90); penpos6(thin_join-fine,0);
y6=y0=2/3bar_height;
y5r=-oo; x5l=.5w-.25u; x6l=x0l;
(x,y5r)=whatever[z5l,z6l]; x5r:=max(x,.5[x5,x2'r]);
make_paths1(rev.l) {{interim superness:=hein_super;
  pulled_arc.e(2',5)}} & z5e{right}...{up}z6e;  % arc
if serifs:
 p1=sloped_serif_path.l(1,2,a,1/3,jut,serif_drop); % upper left serif
 p2=sloped_serif_path.l(3,0,b,1/3,jut,serif_drop); % upper right serif
 p3=z3l--sloped_serif_path.r(4,0',c,1/3,jut,min(oo,serif_drop)); % lower right
else:
 p1=pen_rlend1(up,tiny,down);
 p2=pen_rlend3(up,tiny,down);
 p3=z3l--pen_lrend4(down,tiny,up);
fi
fill p1--combine(rpath1, p3)--p2--lpath1--cycle;
penlabels(1,2,3,4,5); labels(6); endchar;

cmchar "The letter v";
beginchar("v",if serifs: 9.5u# else:9u# fi,x_height#,0);
italcorr x_height#*slant+.25u#;
adjust_fit(serif_fit# if monospace:+\\.5u#,.5u#+ else:,fi\\ serif_fit#);
numeric left_stem,right_stem,outer_jut,alpha;
left_stem=fudged.stem-stem_corr;
right_stem=min(fudged.hair if hefty:-2stem_corr fi,left_stem);
outer_jut=.75jut; x1l=w-x4r=outer_jut; y1=y4=h;
x2-x1=x4-x3; x2l+apex_corr=x3l; y2=y3=-apex_oo;
alpha=diag_ratio(2,right_stem,y1-y2,x4r-x1l-apex_corr);
penpos1(alpha*left_stem,0); penpos2(alpha*left_stem,0);
penpos3(alpha*right_stem,0); penpos4(alpha*right_stem,0);
z0=whatever[z1r,z2r]=whatever[z3l,z4l];
if y0>notch_cut: y0:=notch_cut;
  fill z0+.5right{up}...{z4-z3}diag_end(3l,4l,1,1,4r,3r)
    --diag_end(4r,3r,1,1,2l,1l)--diag_end(2l,1l,1,1,1r,2r){z2-z1}
    ...{down}z0+.5left--cycle; % left and right diagonals
else: fill z0--diag_end(0,4l,1,1,4r,3r)--diag_end(4r,3r,1,1,2l,1l)
    --diag_end(2l,1l,1,1,1r,0)--cycle; fi % left and right diagonals
if serifs: numeric inner_jut; pickup tiny.nib;
 prime_points_inside(1,2); prime_points_inside(4,3);
 if rt x1'r+jut+.5u+1<=lft x4'l-jut: inner_jut=jut;
 else: rt x1'r+inner_jut+.5u+1=lft x4'l-inner_jut; fi
 dish_serif(1',2,a,1/3,outer_jut,b,1/2,inner_jut);  % left serif
 dish_serif(4',3,c,.6,inner_jut,d,1/2,outer_jut)(dark); fi  % right serif
penlabels(0,1,2,3,4); endchar;

cmchar "The letter w";
beginchar("w",13u#,x_height#,0);
italcorr x_height#*slant+.25u#;
adjust_fit(serif_fit#,serif_fit#);
numeric stem[],outer_jut,upper_notch,alpha,mid_corr;
outer_jut=.7jut; x1l=w-x8r=outer_jut;
stem1=fudged.stem-stem_corr;
stem4=min(fudged.hair if hefty:-2stem_corr fi,stem1);
stem2=if hefty:.1[vair,stem4] else: stem4 fi;
stem3=max(stem2,stem1-3stem_corr);
x2-x1=x4-x3=x6-x5=x8-x7; x2l+apex_corr=x3l; x6l+apex_corr=x7l;
y1=y8=h; y2=y3=y6=y7=-apex_oo;
y4=y5=if monospace: vround .6 fi\\ h; upper_notch=y4-notch_cut;
mid_corr=if monospace or hefty:-.3apex_corr else: .5jut fi;
alpha=diag_ratio(4,stem2-stem3+stem4,y1-y2,x8r-x1l+mid_corr-2apex_corr);
penpos1(alpha*stem1,0); penpos2(alpha*stem1,0);
penpos3(alpha*stem2,0); penpos4(alpha*stem2,0);
penpos5(alpha*stem3,0); penpos6(alpha*stem3,0);
penpos7(alpha*stem4,0); penpos8(alpha*stem4,0);
x4l-x1l=floor(x4l-x1l+.5(x5r+mid_corr-x4r)); % |x5r| $\approx$ |x4r+mid_corr|
z23=whatever[z1r,z2r]=whatever[z3l,z4l];
z45=whatever[z3r,z4r]=whatever[z5l,z6l];
z67=whatever[z5r,z6r]=whatever[z7l,z8l];
fill diag_end(1l,2l,1,1,3r,4r){z4-z3}
 ...if y45<upper_notch:(x45,upper_notch)+.5left{up}
   --(x45,upper_notch)+.5right{down} else: z45&z45 fi
 ...{z6-z5}diag_end(5l,6l,1,1,7r,8r)--diag_end(7r,8r,1,1,8l,7l){z7-z8}
 ...if y67>notch_cut:(x67,notch_cut)+.5right{down}
   --(x67,notch_cut)+.5left{up} else: z67&z67 fi
 ...{z5-z6}special_diag_end(6,5,4,3){z3-z4}
 ...if y23>notch_cut:(x23,notch_cut)+.5right{down}
   --(x23,notch_cut)+.5left{up} else: z23&z23 fi
 ...{z1-z2}diag_end(2r,1r,1,1,1l,2l)--cycle;  % diagonals
if serifs: numeric inner_jut[]; pickup tiny.nib;
 prime_points_inside(1,2); prime_points_inside(5,6); prime_points_inside(8,7);
 if monospace: inner_jut1=inner_jut4=1.5jut;
 elseif hefty: inner_jut1=inner_jut4=jut;
 else: fill diag_end(6r,5r,1,1,5l,6l)--.9[z5l,z6l]
   ..{z5-z6}.1[z5r,z6r]--cycle; % middle stem
  inner_jut2=.7inner_jut1; inner_jut4=1.1inner_jut3;
  if rt x1'r+jut+.5u+1<=lft x5'l-.7jut: inner_jut1=jut;
  else: rt x1'r+inner_jut1+.5u+1=lft x5'l-inner_jut2; fi
  if rt x5'r+jut+.5u+1<=lft x8'l-1.1jut: inner_jut3=jut;
  else: rt x5'r+inner_jut3+.5u+1=lft x8'l-inner_jut4; fi
  dish_serif(5',6,e,1/3,inner_jut2,f,1/2,inner_jut3); fi  % middle serif
 dish_serif(1',2,a,1/3,outer_jut,b,1/2,inner_jut1);  % left serif
 dish_serif(8',7,c,.6,inner_jut4,d,1/2,outer_jut)(dark); fi  % right serif
penlabels(0,1,2,3,4,5,6,7,8,23,45,67); endchar;

cmchar "The letter x";
beginchar("x",if serifs:9.5u# else:9u# fi,x_height#,0);
italcorr x_height#*slant-.05u#;
adjust_fit(serif_fit# if monospace:+\\.5u#,.5u#+ else:,fi\\ serif_fit#);
numeric stem[],outer_jut,xjut,alpha[];
stem1=fudged.stem-4stem_corr; stem2=min(fudged.hair,stem1);
outer_jut=.75jut; xjut= if serifs: (stem1-stem2)/4 else: 0 fi;
x1l=x3l+hround(.1u); x2r=x4r-.3u-xjut;
lft x3l=outer_jut+xjut; x4r=w-outer_jut;
y1=y2=h; y3=y4=0;
alpha1=diag_ratio(1,stem1,h,x4r-x1l);
alpha2=diag_ratio(1,stem2,h,x2r-x3l);
penpos1(alpha1*stem1,0); penpos2(alpha2*stem2,0);
penpos3(alpha2*stem2,0); penpos4(alpha1*stem1,0);
if hefty: z0=whatever[z1,z4]=whatever[z2,z3];
 x12=x34=x0; y13=y24=y0;
 z12=whatever[z2l,z3l]; z13=whatever[z2l,z3l];
 z24=whatever[z2r,z3r]; z34=whatever[z2r,z3r];
 forsuffixes $=13,24,34: z$'=.2[z$,z0]; endfor
 fill diag_end(12,1r,.5,1,1l,13')--z13'--diag_end(13',3l,1,.5,3r,34')--z34'
  --diag_end(34',4l,.5,1,4r,24')--z24'
  --diag_end(24',2r,1,.5,2l,12)--z12--cycle; % diagonals
else: fill diag_end(4r,1r,.5,1,1l,4l)
  --diag_end(1l,4l,.5,1,4r,1r)--cycle; % left diagonal
 fill diag_end(2l,3l,.5,1,3r,2r)
  --diag_end(3r,2r,.5,1,2l,3l)--cycle; fi  % right diagonal
if serifs: numeric inner_jut[]; pickup tiny.nib;
 prime_points_inside(1,4); prime_points_inside(2,3);
 prime_points_inside(3,2); prime_points_inside(4,1);
 if rt x1'r+jut+.5u+1<=lft x2'l-jut-xjut: inner_jut1=jut;
 else: rt x1'r+inner_jut1+.5u+1=lft x2'l-inner_jut1-xjut; fi
 if rt x3'r+jut+.5u+1<=lft x4'l-jut-xjut: inner_jut2=jut;
 else: rt x3'r+inner_jut2+.5u+1=lft x4'l-inner_jut2-xjut; fi
 dish_serif(1',4,a,1/3,outer_jut,b,2/3,inner_jut1);  % upper left serif
 dish_serif(4',1,c,2/3,inner_jut2,d,1/3,outer_jut);  % lower right serif
 dish_serif(2',3,e,2/3,inner_jut1+xjut,
  f,1/2,outer_jut+xjut)(dark);  % upper right serif
 dish_serif(3',2,g,1/2,outer_jut+xjut,
  h,2/3,inner_jut2+xjut)(dark); fi  % lower left serif
penlabels(0,1,2,3,4,12,13,24,34); endchar;

cmchar "The letter y";
beginchar("y",if serifs:9.5u# else:9u# fi,x_height#,desc_depth#);
italcorr x_height#*slant+.25u#;
adjust_fit(serif_fit# if monospace:+\\.5u#,.5u#+ else:,fi\\ serif_fit#);
path p[];
numeric left_stem,right_stem,bot_stem,bot_vair,outer_jut;
left_stem=fudged.stem-stem_corr;
right_stem=fudged.hair if hefty:-2stem_corr fi;
bot_stem=fudged.hair if hefty:-8stem_corr fi;
bot_vair=Vround(if serifs: vair else:.5[vair,bot_stem] fi);
outer_jut=.75jut;
x4r=w-outer_jut;
x1l=outer_jut; y1=y4r=h; y2=y3=0; x2l=x3l;
numeric alpha,alpha[]; x9=3u; y9=bot_vair-d-oo;
alpha1=diag_ratio(2,bot_stem,y1-y3,x4r-x1l-apex_corr);
alpha2=diag_ratio(1,bot_stem,y1-y9,x4r-x9);
if alpha1<alpha2: x2l-x1l=x4r-x3r+apex_corr; alpha=alpha1;
else: alpha=alpha2; z3l=whatever[z9,z4r-(alpha*bot_stem,0)]; fi
penpos3(alpha*bot_stem,0); penpos4(alpha*right_stem,0);
alpha3=(y1++(x2l-x1l))/y1;
penpos1(alpha3*left_stem,0); penpos2(alpha3*left_stem,0);
z0=whatever[z1r,z2r]=z4l+whatever*(z3r-z4r);
penpos5(alpha*bot_stem,0); z5r=whatever[z3r,z4r]; y5-.5vair=-.5d;
if y0>notch_cut:   y0:=notch_cut; fi
if serifs:
 numeric light_bulb; light_bulb=hround 7/8[hair,flare];
 penpos6(vair,-90); penpos7(hair,-180); penpos8(light_bulb,-180);
 x6=2u; y6r=-d-oo; y8-.5light_bulb=-.85d; x8r=0;
 make_paths1(rev.r) z3e---z5e...{left}z6e; % arc and bulb
 p1=bulb_path(6,7,8,left);
 numeric inner_jut;
 prime_points_inside(1,2); prime_points_inside(4,3);
 if x1'r+jut+.5u+1<=x4'l-jut: inner_jut=jut;
 else: x1'r+inner_jut+.5u+1=x4'l-inner_jut; fi
 p2=dish_serif_path(1',2,a,1/3,outer_jut,b,1/2,inner_jut);  % left serif
 p3=dish_serif_path(4',3,c,.6,inner_jut,d,1/2,outer_jut)(dark);  % right serif
else:
 penpos6(bot_vair,-90); x6=2.5u; y6r=-d-oo;
 make_paths1(rev.r) stroke z3e---z5e...{left}z6e;  % arc
 pos7(2/3[bot_vair,flare],-85);
 x7l=hround u; y7r=vround-.96d-oo; y7l:=good.y y7l;
 make_paths2(rev.r) term.e(6,7,left,1,4); % arc and terminal
 p1=lpath2.l--lpath2.r; % TODO: should use rounded pen end of fine
 p2=diag_end(2r,1r,1,1,1l,2l);
 p3=diag_end(3r,4r,1,1,rl,0);
fi
if y0>notch_cut:
  fill z0+.5left{up}...{z1r-z2r}p2--z3l & lpath1 & p1 & rpath1--p3
    ...{down}z0+.5right--cycle;
else:
  fill z0--p2--z3l & lpath1 & p1 & rpath1--p3--cycle;
fi
penlabels(0,1,2,3,4,5,6,7,8,9); endchar;

cmchar "The letter z";
beginchar("z",8u#,x_height#,0);
italcorr x_height#*slant-.5serif_fit#-.3u#;
adjust_fit(0,.5serif_fit#);
numeric arm_thickness[],z_stem;
if hefty: arm_thickness1=Vround(vair-vair_corr); arm_thickness2=vair;
 z_stem=.6[vair,stem];
else: arm_thickness1=vair; arm_thickness2=vair'; z_stem=.9[vair,stem]; fi
pickup tiny.nib; rt x1r=rt x2r=w-hround(.05u); lft x3l=lft x4l=0;
top y1=h; y2=min(y1,h-2/3arm_thickness1);
bot y4=0; y3=max(y4,2/3arm_thickness2);
numeric alpha; alpha=diag_ratio(1,z_stem-tiny,y2-y3,x2r-x3l-slant*(y2-y3));
penpos1(alpha*(z_stem-tiny),0); penpos2(alpha*(z_stem-tiny),0);
penpos3(alpha*(z_stem-tiny),0); penpos4(alpha*(z_stem-tiny),0);
pair delta; delta=penoffset z3-z2 of currentpen;
fill top lft z1l--z2l+delta---z3l+delta..lft z3l---lft z4l..bot z4l
 ---bot rt z4r--z3r-delta---z2r-delta..rt z2r---rt z1r..top z1r
 ---cycle;  % diagonal
pickup crisp.nib; pos5(arm_thickness1,90); pos6(hair,180);
top y5r=h; x5=x1; x6r=x3l+hround(.25u); y6=good.y(y5l-beak/1.4)-eps;
arm(5,6,a,beak_darkness,-.4beak_jut);  % upper arm and beak
pos7(arm_thickness2,-90); pos8(hair,0);
bot y7r=0; x7=x4; rt x8r=w; y8=good.y(y7l+beak/1.2)+eps;
arm(7,8,b,beak_darkness,.6beak_jut);  % lower arm and beak
penlabels(1,2,3,4,5,6,7,8); endchar;
