% THIS IS THE OFFICIAL COMPUTER MODERN SOURCE FILE italig.mf BY D E KNUTH.
% IT MUST NOT BE MODIFIED IN ANY WAY UNLESS THE FILE NAME IS CHANGED!

% This file describes five italic ligatures that begin with `f'
% and puts them in code positions \0013--\0017.

numeric itc; % modified italic correction on `f' and `ff'
itc=(asc_height#-x_height#)*slant+.75u#;

ligtable "f": "i"=:oct"014", "f"=:oct"013", "l"=:oct"015",
 "'" kern itc, "?" kern itc, "!" kern itc, ")" kern itc, "]" kern itc;
ligtable oct"013": "i"=:oct"016", "l"=:oct"017",
 "'" kern itc, "?" kern itc, "!" kern itc, ")" kern itc, "]" kern itc;

numeric f_flare; f_flare=.8small_flare;

cmchar "Italic ligature ff";
beginchar(oct"200",5u#+max(1.5u#,stem#)+max(3.5u#,2flare#)+2letter_fit#,
 asc_height#,desc_depth#);
italcorr asc_height#*slant+.75u#;
adjust_fit(0,0); pickup fine.nib;
z98-z88=z99-z89=z14-z4=z15-z5=(4.6u+2letter_fit,0);
.5[x88,x99]=.5w-.25u; x89-x88=u; y98=h; y99=-d;
numeric theta; theta=angle(z88-z89);
pos0(f_flare,0); pos1(hair,0);
pos3(.5[hair,stem],180); pos4(stem,180);
pos5(stem,0); pos6(.5[hair,stem],0);
pos8(hair,-180); pos9(f_flare,-180);
.1[x0,x0r]=x14r; x9r=0;
y2r=h; y7r=-d;
y4=x_height; y5=.5x_height;
z4=whatever[z88,z89]; z5=whatever[z88,z89];
x3=.8[x2+x4-x88,x88]; x6=.8[x7+x5-x89,x89]; y3=.8[y4,y2]; y6=.8[y5,y7];
pos10(f_flare,0); pos11(hair,0);
pos13(.5[hair,stem],180); pos14(stem,180);
pos15(stem,0); pos16(.5[hair,stem],0);
pos18(hair,-180); pos19(f_flare,-180);
x10r=w; x15r-x19r=x5r-x9r;
x13=.8[x12+x14-x98,x98]; x16=.8[x17+x15-x99,x99];
y2r=y12r; y3=y13; y6=y16; y7r=y17r;
set_bulb_points(vair,.55,.7, 4,2,1,0);
p0=bulb_path(2,1,0,right);
set_bulb_points(.8vair,.55,.75, 5,7,8,9);
p9=bulb_path(7,8,9,left);
make_paths2(rev.l) z2e{left}...{z89-z88}z4e;  % upper arc
make_paths5(rev.r) z5e{z89-z88}...{left}z7e;  % lower arc
set_bulb_points(vair,.55,.75, 14,12,11,10);
p10=bulb_path(12,11,10,right);
set_bulb_points(.8vair,.55,.75, 15,17,18,19);
p19=bulb_path(17,18,19,left);
make_paths12(rev.l) z12e{left}...{z99-z98}z14e;  % upper arc
make_paths15(rev.r) z15e{z99-z98}...{left}z17e;  % lower arc
pos20(bar,90); pos21(bar,90);
y20r=y21r=x_height;
x20=x4r-.5stem-1.1u; x21=x14l+.5stem+1.3u;
z4'l=whatever[z4r,z5l]; z4'r=whatever[z4l,z5r]; y4'l=y4'r=y20l;
z14'l=whatever[z14r,z15l]; z14'r=whatever[z14l,z15r]; y14'l=y14'r=y20l;
fill pen_rlend20(left,crisp,right)--z4'l--lpath5--p9--rpath5--z4'r
 --z14'l--lpath15--p19--rpath15--z14'r
 --pen_lrend21(right,crisp,left)--lpath12 & p10
 & combine(rpath12,subpath(4,infinity) of p0) & rpath2--cycle;
unfill lpath2 & combine(subpath(0,4) of p0,rpath12)--cycle;
penlabels(0,1,2,3,4,4',5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,
 20,21,88,89,98,99); endchar;

cmchar "Italic ligature fi";
beginchar(oct"231",6.5u#+.5max(1.5u#,stem#)+.5max(3.5u#,2flare#)
 +2letter_fit#,asc_height#,desc_depth#);
italcorr max(asc_height#*slant+.5stem#-2u#,1/3x_height#*slant+.5hair#+.5u#);
adjust_fit(0,0); pickup fine.nib;
.5[x88,x89]=.5(w-4u-2letter_fit); x89-x88=u; y88=h; y89=-d;
numeric theta; theta=angle(z88-z89);
pos0(f_flare,0); pos1(hair,0);
pos3(.5[hair,stem],180); pos4(stem,180);
pos5(stem,0); pos6(.5[hair,stem],0);
pos8(hair,-180); pos9(f_flare,-180);
pos11(stem,0); x11r=hround(w-3.2u+.5stem);
pos14(.9hair,155);
x12=x11; x14l=w; y14r=.35x_height;
hook_out_points(12,13,14);  % closing hook
p14=hook_out_path(fine,12,13,14);
x0r=.5[x11,x11r]; x9r=0;
y7r=-d; y2r=h;
y4=x_height; y5=.5x_height;
z4=whatever[z88,z89]; z5=whatever[z88,z89];
x3=.8[x2+x4-x88,x88]; x6=.8[x7+x5-x89,x89]; y3=.8[y4,y2]; y6=.8[y5,y7];
set_bulb_points(vair,.55,.75, 4,2,1,0);
p0=bulb_path(2,1,0,right);
set_bulb_points(.8vair,.55,.75, 5,7,8,9);
p9=bulb_path(7,8,9,left);
make_paths2(rev.l) z2e{left}...{z89-z88}z4e;  % upper arc
make_paths5(rev.r) z5e{z89-z88}...{left}z7e;  % lower arc
y11=x_height;
pos20(bar,90); pos21(bar,90);
y20r=y21r=x_height;
x20=x4r-.5stem-1.1u;
z4'l=whatever[z4r,z5l]; z4'r=whatever[z4l,z5r]; y4'l=y4'r=y20l;
fill pen_rlend20(left,crisp,right)--z4'l--lpath5--p9--rpath5--z4'r
 --(x12l,y20l)--p14--pen_point(12r,11r,tiny,4l)--lpath2 & p0 & rpath2--cycle;
penlabels(0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,20,21,88,89); endchar;

cmchar "Italic ligature fl";
beginchar(oct"234",7u#+.5max(1.5u#,stem#)+.5max(3.5u#,2flare#)
 +2letter_fit#,asc_height#,desc_depth#);
italcorr max(asc_height#*slant+.5stem#-2u#,1/3x_height#*slant+.5hair#+.5u#);
adjust_fit(0,0); pickup fine.nib;
.5[x88,x89]=.5(w-4u-2letter_fit); x89-x88=u; y88=h; y89=-d;
numeric theta; theta=angle(z88-z89);
pos0(f_flare,0); pos1(hair,0);
pos3(.5[hair,stem],180); pos4(stem,180);
pos5(stem,0); pos6(.5[hair,stem],0);
pos8(hair,-180); pos9(f_flare,-180);
pos11(stem,0); x11r=hround(w-3.2u+.5stem); .1[x0,x0r]=x11l;
pos14(.9hair,155);
x12=x11; x14l=w; y14r=.35x_height;
hook_out_points(12,13,14);  % closing hook
p14=hook_out_path(fine,12,13,14);
x9r=0;
y2r=h; y7r=-d;
y4=x_height; y5=.5x_height;
z4=whatever[z88,z89]; z5=whatever[z88,z89];
x3=.8[x2+x4-x88,x88]; x6=.8[x7+x5-x89,x89]; y3=.8[y4,y2]; y6=.8[y5,y7];
set_bulb_points(vair,.55,.75, 4,2,1,0);
p0=bulb_path(2,1,0,right);
set_bulb_points(.8vair,.55,.75, 5,7,8,9);
p9=bulb_path(7,8,9,left);
make_paths2(rev.l) z2e{left}...{z89-z88}z4e;  % upper arc
make_paths5(rev.r) z5e{z89-z88}...{left}z7e;  % lower arc
y11=h;
pos20(bar,90); pos21(bar,90);
y20r=y21r=x_height;
x20=x4r-.5stem-1.1u; x21=x11l;
z4'l=whatever[z4r,z5l]; z4'r=whatever[z4l,z5r]; y4'l=y4'r=y20l;
p1=sloped_serif_path.l(11,12,a,1/3,.2jut,serif_drop);  % serif
fill pen_rlend20(left,crisp,right)--z4'l--lpath5--p9--rpath5--z4'r
 --(x12l,y20l)--p14
 --combine(p1,subpath(4,infinity) of p0) & rpath2--cycle;
unfill lpath2 & combine(subpath(0,4) of p0,z11l--z21r)--cycle;
penlabels(0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,20,21,88,89); endchar;

cmchar "Italic ligature ffi";
beginchar(oct"201",11.75u#+.5max(1.5u#,stem#)+.5max(3.5u#,2flare#)
 +4letter_fit#,asc_height#,desc_depth#);
italcorr max(asc_height#*slant+.5stem#-2u#,1/3x_height#*slant+.5hair#+.5u#);
adjust_fit(0,0);
z98-z88=z99-z89=z14-z4=z15-z5=(4.6u+2letter_fit,0);
numeric theta,mid_f;
mid_f=.5[x88,x99]=.5(w-4.25u-2letter_fit); x89-x88=u;
y98=h; y99=-d; theta=angle(z88-z89);
pos21(stem,0); x21r=hround(w-3.2u+.5stem); y21=x_height;
pos24(.9hair,155);
x22=x21; x24l=w; y24r=.35x_height; hook_out_points(22,23,24);  % closing hook
p24=hook_out_path(fine,22,23,24);
pos0(f_flare,0); pos1(hair,0);
pos3(.5[hair,stem],180); pos4(stem,180);
pos5(stem,0); pos6(.5[hair,stem],0);
pos8(hair,-180); pos9(f_flare,-180);
.1[x0,x0r]=x14r; x9r=0;
y2r=h; y7r=-d;
y4=x_height; y5=.5x_height;
z4=whatever[z88,z89]; z5=whatever[z88,z89];
x3=.8[x2+x4-x88,x88]; x6=.8[x7+x5-x89,x89]; y3=.8[y4,y2]; y6=.8[y5,y7];
pos10(f_flare,0); pos11(hair,0);
pos13(.5[hair,stem],180); pos14(stem,180);
pos15(stem,0); pos16(.5[hair,stem],0);
pos18(hair,-180); pos19(f_flare,-180);
x11r=.5[x21,x21r]; x15r-x19r=x5r-x9r;
x13=.8[x12+x14-x98,x98]; x16=.8[x17+x15-x99,x99];
y2r=y12r; y3=y13; y6=y16; y7r=y17r;
x10r=.5[x21,x21r];
pos30(bar,90); pos31(bar,90);
y30r=y31r=x_height;
x30=x4r-.5stem-1.1u; x31=x21l;
set_bulb_points(vair,.55,.7, 4,2,1,0);
p0=bulb_path(2,1,0,right);
set_bulb_points(.8vair,.55,.75, 5,7,8,9);
p9=bulb_path(7,8,9,left);
make_paths2(rev.l) z2e{left}...{z89-z88}z4e;  % upper arc
make_paths5(rev.r) z5e{z89-z88}...{left}z7e;  % lower arc
set_bulb_points(vair,.55,.75, 14,12,11,10);
p10=bulb_path(12,11,10,right);
set_bulb_points(.8vair,.55,.75, 15,17,18,19);
p19=bulb_path(17,18,19,left);
make_paths12(rev.l) z12e{left}...{z99-z98}z14e;  % upper arc
make_paths15(rev.r) z15e{z99-z98}...{left}z17e;  % lower arc
z4'l=whatever[z4r,z5l]; z4'r=whatever[z4l,z5r]; y4'l=y4'r=y30l;
z14'l=whatever[z14r,z15l]; z14'r=whatever[z14l,z15r]; y14'l=y14'r=y30l;
fill pen_rlend30(left,crisp,right)--z4'l--lpath5--p9--rpath5--z4'r
 --z14'l--lpath15--p19--rpath15--z14'r--z31l--p24--z21r
 --lpath12 & p10
 & combine(rpath12,subpath(4,infinity) of p0) & rpath2--cycle;
unfill lpath2 & combine(subpath(0,4) of p0,rpath12)--cycle;
penlabels(0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,
 20,21,22,23,24,30,31,88,89,98,99); endchar;

cmchar "Italic ligature ffl";
beginchar(oct"202",12u#+.5max(1.5u#,stem#)+.5max(3.5u#,2flare#)
 +4letter_fit#,asc_height#,desc_depth#);
italcorr max(asc_height#*slant+.5stem#-2u#,1/3x_height#*slant+.5hair#+.5u#);
adjust_fit(0,0);
z98-z88=z99-z89=z14-z4=z15-z5=(4.6u+2letter_fit,0);
numeric theta,mid_f;
mid_f=.5[x88,x99]=.5(w-4.3u-2letter_fit); x89-x88=u;
y98=h; y99=-d; theta=angle(z88-z89);
pos21(stem,0); x21r=hround(w-3.2u+.5stem);
pos24(.9hair,155);
x22=x21; x24l=w; y24r=.35x_height;
hook_out_points(22,23,24);  % closing hook
p24=hook_out_path(fine,22,23,24);  % closing hook
pos0(f_flare,0); pos1(hair,0);
pos3(.5[hair,stem],180); pos4(stem,180);
pos5(stem,0); pos6(.5[hair,stem],0);
pos8(hair,-180); pos9(f_flare,-180);
.1[x0,x0r]=x14r; x9r=0;
y2r=h; y7r=-d;
y4=x_height; y5=.5x_height;
z4=whatever[z88,z89]; z5=whatever[z88,z89];
x3=.8[x2+x4-x88,x88]; x6=.8[x7+x5-x89,x89]; y3=.8[y4,y2]; y6=.8[y5,y7];
pos10(f_flare,0); pos11(hair,0);
pos13(.5[hair,stem],180); pos14(stem,180);
pos15(stem,0); pos16(.5[hair,stem],0);
pos18(hair,-180); pos19(f_flare,-180);
x15r-x19r=x5r-x9r;
x13=.8[x12+x14-x98,x98]; x16=.8[x17+x15-x99,x99];
y2r=y12r; y3=y13; y6=y16; y7r=y17r;
.1[x10,x10r]=x21l;
pos30(bar,90); pos31(bar,90);
y30r=y31r=x_height;
x30=x4r-.5stem-1.1u; x31=x21l;
set_bulb_points(vair,.55,.7, 4,2,1,0);
p0=bulb_path(2,1,0,right);
set_bulb_points(.8vair,.55,.75, 5,7,8,9);
p9=bulb_path(7,8,9,left);
make_paths2(rev.l) z2e{left}...{z89-z88}z4e;  % upper arc
make_paths5(rev.r) z5e{z89-z88}...{left}z7e;  % lower arc
set_bulb_points(vair,.55,.75, 14,12,11,10);
p10=bulb_path(12,11,10,right);
set_bulb_points(.8vair,.55,.75, 15,17,18,19);
p19=bulb_path(17,18,19,left);
make_paths12(rev.l) z12e{left}...{z99-z98}z14e;  % upper arc
make_paths15(rev.r) z15e{z99-z98}...{left}z17e;  % lower arc
z4'l=whatever[z4r,z5l]; z4'r=whatever[z4l,z5r]; y4'l=y4'r=y30l;
z14'l=whatever[z14r,z15l]; z14'r=whatever[z14l,z15r]; y14'l=y14'r=y30l;
y21=h;
p1=sloped_serif_path.l(21,22,a,1/3,.2jut,serif_drop);  % serif
fill pen_rlend30(left,crisp,right)--z4'l--lpath5--p9--rpath5--z4'r
 --z14'l--lpath15--p19--rpath15--z14'r--z31l--p24
 --combine(p1,subpath(4,infinity) of p10)
 & combine(rpath12,subpath(4,infinity) of p0) & rpath2--cycle;
unfill lpath2 & combine(subpath(0,4) of p0,rpath12)--cycle;
unfill lpath12 & combine(subpath(0,4) of p10,z21l--z31r)--cycle;
penlabels(0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,
 20,21,22,23,24,30,31,88,89,98,99); endchar;
