% THIS IS THE OFFICIAL COMPUTER MODERN SOURCE FILE italp.mf BY D E KNUTH.
% IT MUST NOT BE MODIFIED IN ANY WAY UNLESS THE FILE NAME IS CHANGED!

% This file contains `\$' and `\&' and `?' in the so-called italic style.

% Codes \0044, \0046, and \0077 are generated, as well as code |spanish_query|
% (for a Spanish question mark) if that code value is known.

cmchar "Dollar sign";
beginchar("$",9u#,fig_height#,body_height#-asc_height#);
italcorr fig_height#*slant-.5u#; % same as for digits
adjust_fit(0,0);
numeric rel_slant; rel_slant=22u;
numeric theta; theta=90-angle(rel_slant,asc_height);
slope:=-asc_height/rel_slant;
pos2(vair,-90);
pos0(stem,theta); pos7(slab,-90);
x0=.5w; .5[x2,x7]=x0; x7-x2=6u*slant; y2l=h+oo; y7r=-.5[oo,o];
y0=.52[y7r,y2l]; x3l=.2u; x6r=w;
x3r-x3l=x6r-x6l=hround .35[slab,cap_stem]-fine;
ellipse_set(2l,3l,4l,0l); ellipse_set(2r,3r,4r,0r); y3=y3r;
ellipse_set(7l,6l,5l,0l); ellipse_set(7r,6r,5r,0r); y6=y6r;
make_paths3456(rev.r) z3e{down}..{z5e-z4e}z0e..z6e{down};  % main stroke
if serifs: pos1(hair,0); pos8(hair,180);
 pos10(.5[small_flare,flare],0); pos9(.5[small_flare,flare],180);
 y2'r=y2l; x10r=hround(w-.3u);
 set_bulb_points(y2l-y2r,.45,.66, 3,2',1,10);
 p10=bulb_path(2',1,10,right);  % upper arc and bulb
 z3'r=z3l;z3'l=z3r;
 make_paths23(rev.l) super_arc.e(2',3');
 y7'r=y7r; x9r=0; 
 set_bulb_points(y7l-y7r,.45,.75, 6,7',8,9);
 p9=bulb_path(7',8,9,left);
 make_paths67(rev.r) super_arc.e(6,7');
else: pos1(.6[slab,flare],-100); pos8(flare,-100);
 rt x1l=hround(w-1.3u); lft x8r=hround .8u;
 top y1l=vround .92asc_height+oo; bot y8r=vround .1asc_height-oo;
 filldraw stroke term.e(2,1,right,.9,4);  % upper arc and terminal
 filldraw stroke term.e(7,8,left,1,4); fi  % lower arc and terminal
pos12(bar,0); pos11(bar,0);
x12l=x11l=hround(.51w-.5bar); y12=y2l+2vair; y11=y7r-3.5vair;
fill p10 & rpath23 & lpath3456 & lpath67 & p9 & rpath67 & rpath3456 & lpath23
 & cycle;
fill pen_rlend12(up,crisp,down)--pen_lrend11(down,crisp,up)--cycle;
penlabels(0,1,2,3,4,5,6,7,8,9,10,11,12); endchar;

cmchar "Italic ampersand";
beginchar("&",14u#,asc_height#,0);
italcorr x_height#*slant-serif_fit# - if serifs: .4u# else: 1.5u# fi;
adjust_fit(0,serif_fit#);
pos2(.3[thick_vair,stem],-90); x2=3.3u; x2l:=4.5u; y2r=-o;
if serifs:
 pair amp_stem_dir; amp_stem_dir=dir 70;
 pos1(thick_hair,-45); z1=(w-3.5u,.4[bar_height,x_height]);
 pos1'(thick_hair,angle(z1-z2)-90);
 z1'=.5[z1,z2] + (2thick_hair,0) rotated (angle(z1-z2)-90);
 p1=z2r{right}..z1'r..{amp_stem_dir}z1r;
 p2=z1l{-amp_stem_dir}..z1'l..z2l{left};
else: pickup fine.nib; pos1(.25[slab,flare],-15); rt x1r=hround(w-2u);
 y1r=good.y .75[bar_height,x_height]; x1l:=good.x x1l; y1l:=good.y y1l;
 top z2'l=(x2l,tiny.top y2l); bot z2'r=(x2r,tiny.bot y2r);
 filldraw stroke term.e(2',1,right,1,4); fi  % short diagonal and terminal
numeric slope,theta,reduced_hair;
slope=(h-2vair-slab)/17.5u; theta=angle(-slope,1);
reduced_hair=max(tiny.breadth,hround(fudged.hair if hefty:-2stem_corr fi));
x3r=0; x5=.5[x3r,x6l]; x6r=hround .57w;
x3l-x3r=curve; pos6(.5[thick_hair,stem],180);
pos5(vair,theta); y5=.55h;
ellipse_set(2l,3l,4l,5l); ellipse_set(2r,3r,4r,5r);
pos7(vair,270); y7l=h; x7=.6[x6r,x8r];
pos8(stem*sqrt(2),45); x8=3.25u; y8r=y6;
ellipse_set(7l,6l,5',5l);
make_paths2(rev.r) z2e{left}...z3e{up}...z4e---z5e...{up}z6e
 ...z7e{left}...z8e{down};  % bowls
pos10(.5[thick_vair,stem],90); x10=w-2.5u; y10l=-o;
x10r:=x10+.5u;
pos9(curve,angle(z8-z10)-90);
z9=.5[z8,z10]+(.8stem,0) rotated(angle(z8-z10)+90);
make_paths8(rev.r) z8e{down}...z9e{z10-z8}...{right}z10e;  % long diagonal
if serifs:
 pos11(fudged.hair,180); x11l=w; y11=.63bar_height;
 pair amp_term_dir; amp_term_dir=dir 88;
 p10=z10l{right}
  ...{amp_term_dir}pen_lrend11(amp_term_dir,fine,-amp_term_dir){-amp_term_dir}
  ...z10r{left};
 pos0(small_flare,0); pos100(hair,0);
 y101r=.95x_height; x0r=w-.4u;
 z1''r=z1l; z1''l=z1r;
 set_bulb_points(vair,.7,1, 1'',101,100,0);
 p0=bulb_path(101,100,0,right);
else: z10'r=z10l; z10'l=z10r;
 pos11(Vround .5[slab,flare],90);
 x11=hround(r-letter_fit-u); y11l=vround .07bar_height-o;
 filldraw stroke term.e(10',11,right,1,4); fi  % terminal
fill p0..combine(combine(p2,rpath8),lpath2)
 & combine(lpath8,rpath2) & combine(p1,lpath8) & p10
 & combine(rpath8,p1)..cycle;
unfill combine(combine(lpath2,lpath8),p2) & cycle;
unfill combine(rpath2,rpath8) & cycle;
penlabels(0,1,1',2,3,4,5,6,7,8,9,10,11,101); endchar;

cmchar "Italic question mark";
beginchar("?",9u#,asc_height#,0);
italcorr asc_height#*slant-u#;
adjust_fit(0,0); pickup tiny.nib; pos10(dot_size,0); pos11(dot_size,90);
x10l=hround(.5w+.3u-.5dot_size); y11l=-dot_o; z10=z11; dot(10,11);  % dot
numeric theta; theta=90+angle(12u,h); slope:=h/12u;
pos1(hair,10); pos2(hair,0); pos3(vair,-90);
pos0(ess,theta); x0=x3+.3u; y0=1/3[y8,y3];
x3=.4w;
y3l=h+.5oo;
x2l=0; y2=.5[y1l,y3l];
y1l=x_height+hair; x1r=u;
pos8(vair,-90);
fill stroke z1e..z2e{up}..z3e{right};
y8r=.27[top y11r,x_height]+1;
x4l=w; x7r=hround 1/4w;
x4l-x4r=stem; x7l-x7r=hround .5[vair,vstem]-fine;
pos9(hair,-20); x9r=hround(.85w+.5hair); y9=good.y .5[y8,y0];
z8=.5[(x7r,y9),z9r]+whatever*(-slant,1);
ellipse_set(3l,4l,5l,0l);
ellipse_set(8l,7l,6l,0l);
numeric theta; theta=90+angle(9u,h); slope:=h/9u;
ellipse_set(3r,4r,5r,0r); y4=y4r;
ellipse_set(8r,7r,6r,0r); y7=y7r;
fill stroke super_arc.e(3,4) & z4e{down}
 ..z5e---z6e..z7e{down} & super_arc.e(7,8)...{up}z9e;  % main stroke
penlabels(0,1,2,3,4,5,6,7,8,9,10,11); endchar;

