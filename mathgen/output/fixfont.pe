#!/usr/local/bin/fontforge

Open($1)

weight=$2
style=$3

copyright="Copyright 2014 Charles Duan. Based on METAFONT Computer Modern " + \
    "sources by Donald Knuth. This Font Software is licensed under the " + \
    "SIL Open Font License, Version 1.1. The license is available at " + \
    "http://scripts.sil.org/OFL. The name Century Expanded is " + \
    "being used nominatively to describe the design of the font, and " + \
    "should not be taken to indicate an association with any commercial " + \
    "type foundry."

famname="Century Expanded CD"
psname="CenturyExpandedCD"

if (style == "")
    fullstyle = psstyle = weight
elseif (weight == "Regular")
    fullstyle = psstyle = style
else
    fullstyle = weight + " " + style
    psstyle = weight + style
endif

if (style == "Italic")
    SetItalicAngle(-16)
endif

SetFontNames(psname + "-" + psstyle, \
    famname, famname + "-" + fullstyle, \
    weight, copyright)
SetTTFName(0x409, 0, copyright)
SetTTFName(0x409, 1, famname)
SetTTFName(0x409, 2, fullstyle)
SetTTFName(0x409, 4, psname + "-" + psstyle)
SetMacStyle(-1)

AddLookup("Ligatures", "gsub_ligature", 0, [["liga", [["latn", ["dflt"]]]]]);
AddLookupSubtable("Ligatures", "Ligatures 0");
Select("f_f_i"); AddPosSub("Ligatures 0", "f f i");
               AddPosSub("Ligatures 0", "f_f i");
               AddPosSub("Ligatures 0", "f f_i");
Select("f_f_l"); AddPosSub("Ligatures 0", "f f l");
               AddPosSub("Ligatures 0", "f_f l");
               AddPosSub("Ligatures 0", "f f_l");
Select("f_f");  AddPosSub("Ligatures 0", "f f");
Select("f_i");  AddPosSub("Ligatures 0", "f i");
Select("f_l");  AddPosSub("Ligatures 0", "f l");

AddLookup("Caps to Small Caps", "gsub_single", 0, \
    [["c2sc", [["latn", ["dflt"]]]]]);
AddLookupSubtable("Caps to Small Caps", "Caps to Small Caps 0");
AddLookup("Small Caps", "gsub_single", 0, \
    [["smcp", [["latn", ["dflt"]]]], ["ss01", [["latn", ["dflt"]]]]]);
AddLookupSubtable("Small Caps", "Small Caps 0");

i=Ord('A',0);
while (i<=Ord('Z',0))
    Select(i);
    AddPosSub("Caps to Small Caps 0", Chr(i+32) + ".sc");
    Select(i+32);
    AddPosSub("Small Caps 0", Chr(i+32) + ".sc");
    ++i;
endloop

Generate($1:r + ".otf")
