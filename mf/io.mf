mode_setup;

em#:=10pt#; cap# := 7pt#;
thin# := 1/3pt#; thick# := 5/6pt#;
o# := 1/5pt#;
define_pixels(em,cap);
define_blacker_pixels(thin,thick);
define_corrected_pixels(o);
curve_sidebar = round 1/18em;
beginchar("O", 0.8em#, cap#, 0); "The letter O";
penpos 1(thick,10); penpos 2(.1[thin,thick],90 - 10);
penpos 3(thick, 180+10); penpos 4(thin, 270 - 10);
x1l = w - x3l = curve_sidebar; x2 = x4 = .5 w;
y1 = .49 h; y2l = -o; y3 = .51 h; y4l = h + o;
penstroke z1e{down} .. z2e{right} .. z3e{up} .. z4e{left} .. cycle;
penlabels(1,2,3,4); endchar;

def test_I(expr code, trial_stem, trial_width) =
stem# := trial_stem * pt#; define_blacker_pixels(stem);
beginchar(code, trial_width * em#, cap#, 0); "The letter I";
penpos 1(stem, 15); penpos 2(stem, 12); penpos 3(stem, 10);
x1=x2=x3=.5 w; y1 = h; y2 = .55h; y3=0; x2l := 1/6[x2l, x2];
penstroke z1e .. z2e{down} .. z3e;
penlabels(1, 2, 3); endchar; enddef;
